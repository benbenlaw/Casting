import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.165'
    id 'net.darkhax.curseforgegradle' version '1.1.24'
    id "com.modrinth.minotaur" version "2.+"
}

version = "${minecraft_version}-${mod_version}"
group = mod_group_id

repositories {
    mavenLocal()

    maven {
        name = "Curse Maven"
        url = uri("https://cursemaven.com")
        content {
            includeGroup("curse.maven")
        }
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)


runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        arguments.add('--nogui')
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        arguments.addAll('--mod', project.mod_id, '--all', '--output',
                file('src/generated/resources/').getAbsolutePath(),
                '--existing',
                file('src/main/resources/').getAbsolutePath()
        )
    }
}


sourceSets.main.resources { srcDir 'src/generated/resources' }
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {

    implementation "net.neoforged:neoforge:${neo_version}"

    //BBL Core
    compileOnly "curse.maven:bbl-core-1135181:6900859"
    runtimeOnly "curse.maven:bbl-core-1135181:6900859"

    //Construction Sticks
    compileOnly "curse.maven:construction-sticks-1156098:6641229"
    runtimeOnly "curse.maven:construction-sticks-1156098:6641229"

    //Building Gadgets
    compileOnly "curse.maven:building-gadgets-298187:5968587"
    runtimeOnly "curse.maven:building-gadgets-298187:5968587"

    //Ender IO
    compileOnly "curse.maven:enderio-64578:5771156"
    runtimeOnly "curse.maven:enderio-64578:5771156"

    //JEI
    compileOnly "curse.maven:jei-238222:5846880"
    runtimeOnly "curse.maven:jei-238222:5846880"

    //Mekanism
    compileOnly "curse.maven:mekanism-268560:6327955"
    runtimeOnly "curse.maven:mekanism-268560:6327955"

    //Mekanism Tools
    compileOnly "curse.maven:mekanism-tools-268567:6327959"
    runtimeOnly "curse.maven:mekanism-tools-268567:6327959"

    //All The Ores
    compileOnly "curse.maven:ato-405593:6118610"
    runtimeOnly "curse.maven:ato-405593:6118610"

    //mmmmmmmmmmmm
    compileOnly "curse.maven:mmmmmmmmmmmm-225738:6237115"
    runtimeOnly "curse.maven:mmmmmmmmmmmm-225738:6237115"

    //selene
    compileOnly "curse.maven:selene-499980:6391012"
    runtimeOnly "curse.maven:selene-499980:6391012"

    //observable
    compileOnly "curse.maven:observable-509575:5733591"
    runtimeOnly "curse.maven:observable-509575:5733591"

    //spark
    compileOnly "curse.maven:spark-361579:6225208"
    runtimeOnly "curse.maven:spark-361579:6225208"

    //arch
    compileOnly "curse.maven:architectury-api-419699:5786327"
    runtimeOnly "curse.maven:architectury-api-419699:5786327"

    //Kotlin for Forge
    compileOnly "curse.maven:kotlin-for-forge-351264:6497906"
    runtimeOnly "curse.maven:kotlin-for-forge-351264:6497906"

    //KubeJS
    compileOnly "curse.maven:kubejs-238086:5631423"
    runtimeOnly "curse.maven:kubejs-238086:5631423"

    //Rhino
    compileOnly "curse.maven:rhino-416294:5650008"
    runtimeOnly "curse.maven:rhino-416294:5650008"

    compileOnly "org.appliedenergistics:guideme:${project.guideme_version}:api"
    runtimeOnly "org.appliedenergistics:guideme:${project.guideme_version}"

}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {

        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}


tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group "modpublishing"

    apiToken = System.getenv("curseforge_token").toString()

    def projectId = 1052163
    def mainFile = upload(projectId, jar)

    mainFile.changelog = changelog
    mainFile.addOptional("jei") // JEI
    mainFile.addRequirement("bbl-core")
    mainFile.releaseType = "release"
    mainFile.gameVersions.addAll("1.21", "1.21.1")
}

tasks.getByName("modrinth").setGroup("modpublishing")

modrinth {
    token = System.getenv("modrinth_token").toString()
    projectId = "YadXQ97f"
    versionType = "release"
    versionName = "casting-" + "$minecraft_version" + "-" + "$mod_version"
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!

    gameVersions.addAll("1.21", "1.21.1")

    loaders = ["neoforge"]
    changelog = project.changelog
    dependencies {
        optional.project "jei"
        required.project "bbl-core"

    }
}
